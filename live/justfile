workspaces := ""

# Displays available recipes by running `just -l`.
setup:
  #!/usr/bin/env bash
  just -l

# Run the dev server
dev *args:
  #!/usr/bin/env bash
  run_cmd() {
    echo "$@" 
    "$@"
  }

  dev() {
    run_cmd bun run vuepress dev "$@"
  }

  if [ -z "{{args}}" ]; then
    dev
  else 
    dev "{{args}}"
  fi

# Run the dev server without cacheing
dev-no-cache:
  just dev --no-cache

deploy:
  just set-symlinks
  bash scripts/publish.sh

# Alias for dev
serve:
  just dev

serve-no-cache:
  trap 'exit 0' SIGINT; bun run vuepress dev --no-cache

# Install dependencies
install: 
  bun install

# Build TypeScript and then run vuepress build
build:
  tsc
  bun run vuepress build

# Run vuepress build (only)
build-short: 
  bun run vuepress build

# Compile TypeScript to JS.
tsc:
  tsc

# Reset expected symlinks for the site.
set-symlinks:
  #!/usr/bin/env bash

  vuepress_dir="src-vuepress"
  public_dir="$vuepress_dir/public"

  if [ ! -d "$public_dir" ]; then 
    echo "$public_dir does not exist" >&2
    exit 1
  fi

  # Note that symlinking with relative paths will cause nested symlinks on
  # consecutive calls.
  ln -sfn $vuepress_dir .vuepress
  ln -sfn $public_dir public
  # --no-dereference (-n): Treats the existing symlink to a directory as a
  # regular file so that it will be overwritten in the case of a collision
  # instead of adding a nested symlink. 

copy-assets:
  bash copy-assets.sh
